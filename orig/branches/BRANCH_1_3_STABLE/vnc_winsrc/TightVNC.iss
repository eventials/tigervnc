; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=TightVNC
AppVerName=TightVNC 1.3.9
AppVersion=1.3.9
AppPublisher=TightVNC Group
AppPublisherURL=http://www.tightvnc.com/
AppSupportURL=http://www.tightvnc.com/support.html
AppUpdatesURL=http://www.tightvnc.com/download.html
DefaultDirName={pf}\TightVNC
DefaultGroupName=TightVNC
AllowNoIcons=no
InfoBeforeFile=InstInfo.rtf
Compression=zip/9
WindowVisible=no
DisableStartupPrompt=yes
BackColor=clBlack
BackColor2=clBlue
DirExistsWarning=no
ChangesAssociations=yes

[Components]
Name: "server"; Description: "TightVNC Server"; Types: full compact custom;
Name: "viewer"; Description: "TightVNC Viewer"; Types: full compact custom;
Name: "webdoc"; Description: "Web pages and documentation"; Types: full custom;

[Files]
Source: "WinVNC.exe"; DestDir: "{app}"; Flags: ignoreversion restartreplace; Components: server; MinVersion: 4.1,4.0
Source: "VNCHooks.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace; Components: server; MinVersion: 4.1,4.0
Source: "vncviewer.exe"; DestDir: "{app}"; Flags: ignoreversion restartreplace; Components: viewer; MinVersion: 4.1,4.0
Source: "WinVNC_Win95.exe"; DestDir: "{app}"; DestName: WinVNC.exe; Flags: ignoreversion restartreplace; Components: server; OnlyBelowVersion: 4.1,4.0
Source: "VNCHooks_Win95.dll"; DestDir: "{app}"; DestName: VNCHooks.dll; Flags: ignoreversion restartreplace; Components: server; OnlyBelowVersion: 4.1,4.0
Source: "vncviewer_Win95.exe"; DestDir: "{app}"; DestName: vncviewer.exe; Flags: ignoreversion restartreplace; Components: viewer; OnlyBelowVersion: 4.1,4.0
Source: "README.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "LICENCE.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "TightVNC.url"; DestDir: "{app}"; Flags: ignoreversion
Source: "TightVNC-donate.url"; DestDir: "{app}"; Flags: ignoreversion
Source: "Web\*"; DestDir: "{app}\Web"; Flags: ignoreversion; Components: webdoc
Source: "Web\images\*"; DestDir: "{app}\Web\images"; Flags: ignoreversion; Components: webdoc
Source: "Web\logo\*"; DestDir: "{app}\Web\logo"; Flags: ignoreversion; Components: webdoc
Source: "Web\doc\win32\*"; DestDir: "{app}\Web\doc\win32"; Flags: ignoreversion; Components: webdoc
Source: "Web\doc\java\*"; DestDir: "{app}\Web\doc\java"; Flags: ignoreversion; Components: webdoc
Source: "Web\doc\man\*"; DestDir: "{app}\Web\doc\man"; Flags: ignoreversion; Components: webdoc
Source: "Web\doc\unix\*"; DestDir: "{app}\Web\doc\unix"; Flags: ignoreversion; Components: webdoc

[Icons]
Name: "{group}\Launch TightVNC Server";               FileName: "{app}\WinVNC.exe";                                    WorkingDir: "{app}";     Components: server; Tasks: icon\common
Name: "{group}\Show User Settings";                   FileName: "{app}\WinVNC.exe";    Parameters: "-settings";        WorkingDir: "{app}";     Components: server; Tasks: icon\common
Name: "{group}\TightVNC Viewer";                      FileName: "{app}\vncviewer.exe";                                 WorkingDir: "{app}";     Components: viewer; Tasks: icon\common
Name: "{group}\Uninstall TightVNC";                   FileName: "{uninstallexe}";                                      WorkingDir: "{app}";                         Tasks: icon\common
Name: "{group}\Administration\Install VNC Service";   FileName: "{app}\WinVNC.exe";    Parameters: "-install";         WorkingDir: "{app}";     Components: server; Tasks: icon\common
Name: "{group}\Administration\Remove VNC Service";    FileName: "{app}\WinVNC.exe";    Parameters: "-remove";          WorkingDir: "{app}";     Components: server; Tasks: icon\common
Name: "{group}\Administration\Run Service Helper";    FileName: "{app}\WinVNC.exe";    Parameters: "-servicehelper";   WorkingDir: "{app}";     Components: server; Tasks: icon\common
Name: "{group}\Administration\Show Default Settings"; FileName: "{app}\WinVNC.exe";    Parameters: "-defaultsettings"; WorkingDir: "{app}";     Components: server; Tasks: icon\common
Name: "{group}\Documentation\About VNC and TightVNC"; FileName: "{app}\Web\index.html";                                WorkingDir: "{app}\Web"; Components: webdoc; Tasks: icon\common
Name: "{group}\Documentation\Installation and Getting Started"; FileName: "{app}\Web\winst.html";                      WorkingDir: "{app}\Web"; Components: webdoc; Tasks: icon\common
Name: "{group}\Documentation\Licensing Terms";        FileName: "{app}\LICENCE.txt";                                   WorkingDir: "{app}"                        ; Tasks: icon\common
Name: "{group}\Documentation\Make a Donation";        FileName: "{app}\TightVNC-donate.url"                                                                       ; Tasks: icon\common
Name: "{group}\Documentation\TightVNC Web Site";      FileName: "{app}\TightVNC.url"                                                                              ; Tasks: icon\common
Name: "{group}\Documentation\What's New (Detailed Log)"; FileName: "{app}\Web\changelog-win32.html";                   WorkingDir: "{app}\Web"; Components: webdoc; Tasks: icon\common
Name: "{group}\Documentation\What's New (Summary)";   FileName: "{app}\Web\whatsnew.html";                             WorkingDir: "{app}\Web"; Components: webdoc; Tasks: icon\common

Name: "{userstartmenu}\{groupname}\Launch TightVNC Server";               FileName: "{app}\WinVNC.exe";                                    WorkingDir: "{app}";     Components: server; Tasks: icon\user
Name: "{userstartmenu}\{groupname}\Show User Settings";                   FileName: "{app}\WinVNC.exe";    Parameters: "-settings";        WorkingDir: "{app}";     Components: server; Tasks: icon\user
Name: "{userstartmenu}\{groupname}\TightVNC Viewer";                      FileName: "{app}\vncviewer.exe";                                 WorkingDir: "{app}";     Components: viewer; Tasks: icon\user
Name: "{userstartmenu}\{groupname}\Uninstall TightVNC";                   FileName: "{uninstallexe}";                                      WorkingDir: "{app}";                         Tasks: icon\user
Name: "{userstartmenu}\{groupname}\Administration\Install VNC Service";   FileName: "{app}\WinVNC.exe";    Parameters: "-install";         WorkingDir: "{app}";     Components: server; Tasks: icon\user
Name: "{userstartmenu}\{groupname}\Administration\Remove VNC Service";    FileName: "{app}\WinVNC.exe";    Parameters: "-remove";          WorkingDir: "{app}";     Components: server; Tasks: icon\user
Name: "{userstartmenu}\{groupname}\Administration\Run Service Helper";    FileName: "{app}\WinVNC.exe";    Parameters: "-servicehelper";   WorkingDir: "{app}";     Components: server; Tasks: icon\user
Name: "{userstartmenu}\{groupname}\Administration\Show Default Settings"; FileName: "{app}\WinVNC.exe";    Parameters: "-defaultsettings"; WorkingDir: "{app}";     Components: server; Tasks: icon\user
Name: "{userstartmenu}\{groupname}\Documentation\About VNC and TightVNC"; FileName: "{app}\Web\index.html";                                WorkingDir: "{app}\Web"; Components: webdoc; Tasks: icon\user
Name: "{userstartmenu}\{groupname}\Documentation\Installation and Getting Started"; FileName: "{app}\Web\winst.html";                      WorkingDir: "{app}\Web"; Components: webdoc; Tasks: icon\user
Name: "{userstartmenu}\{groupname}\Documentation\Licensing Terms";        FileName: "{app}\LICENCE.txt";                                   WorkingDir: "{app}"                        ; Tasks: icon\user
Name: "{userstartmenu}\{groupname}\Documentation\Make a Donation";        FileName: "{app}\TightVNC-donate.url"                                                                       ; Tasks: icon\user
Name: "{userstartmenu}\{groupname}\Documentation\TightVNC Web Site";      FileName: "{app}\TightVNC.url"                                                                              ; Tasks: icon\user
Name: "{userstartmenu}\{groupname}\Documentation\What's New (Detailed Log)"; FileName: "{app}\Web\changelog-win32.html";                   WorkingDir: "{app}\Web"; Components: webdoc; Tasks: icon\user
Name: "{userstartmenu}\{groupname}\Documentation\What's New (Summary)";   FileName: "{app}\Web\whatsnew.html";                             WorkingDir: "{app}\Web"; Components: webdoc; Tasks: icon\user

[Tasks]
Name: icon; Description: "Create a Start &Menu folder"; GroupDescription: "Additional icons:"; Components: server viewer
Name: icon\common; Description: "&For all users"; GroupDescription: "Additional icons:"; Components: server viewer webdoc; Flags: exclusive
Name: icon\user; Description: "For the current &user only"; GroupDescription: "Additional icons:"; Components: server viewer webdoc; Flags: exclusive unchecked
;Name: quicklaunchicon; Description: "Create a &Quick Launch icon"; GroupDescription: "Additional icons:"; Components: server viewer; Flags: unchecked

Name: associate; Description: "&Associate .vnc files with TightVNC Viewer"; GroupDescription: "File associations:"; Components: viewer
Name: installapp; Description: "&Install TightVNC as an application"; GroupDescription: "Server configuration:"; Components: server; Flags: checkedonce exclusive; OnlyBelowVersion: 0,6.0
Name: installservice; Description: "&Register new TightVNC Server as a system service"; GroupDescription: "Server configuration:"; Components: server; Flags: exclusive; OnlyBelowVersion: 0,6.0
Name: startservice; Description: "&Start or restart TightVNC service"; GroupDescription: "Server configuration:"; Components: server; Flags: exclusive; OnlyBelowVersion: 0,6.0

[Registry]
Root: HKCR; Subkey: ".vnc"; ValueType: string; ValueName: ""; ValueData: "VncViewer.Config"; Flags: uninsdeletevalue; Tasks: associate
Root: HKCR; Subkey: "VncViewer.Config"; ValueType: string; ValueName: ""; ValueData: "VNCviewer Config File"; Flags: uninsdeletekey; Tasks: associate
Root: HKCR; Subkey: "VncViewer.Config\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\vncviewer.exe,0"; Tasks: associate
Root: HKCR; Subkey: "VncViewer.Config\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\vncviewer.exe"" -config ""%1"""; Tasks: associate

[Run]
Filename: "{app}\WinVNC.exe"; Parameters: "-silent -reinstall"; Tasks: installservice startservice
Filename: "net"; Parameters: "start WinVNC"; Tasks: startservice
Filename: "{app}\WinVNC.exe"; Parameters: "-servicehelper"; Tasks: startservice

[UninstallRun]
Filename: "{app}\WinVNC.exe"; Parameters: "-silent -remove";

